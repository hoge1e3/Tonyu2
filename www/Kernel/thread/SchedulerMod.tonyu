extends null;
"field strict";
let _scheduler,_th,_updating,_updateExc;

native Tonyu;
native Key;
native console;
native Math;
native FS;
native Array;

\initSchedulerMod() {
    _scheduler=_scheduler||$Scheduler;
    _th=$Boot.schedule(this,"main",[]);
}

\update() {
    if (!_updating) {
        _updating=true;
        onUpdate();
        _updating=false;
    }   
    if(_thread) {
        if (_scheduler) _scheduler.addToNext(_thread);
        _thread.suspend();
        __await null;
    } else {
        if (_scheduler) _scheduler.checkTimeout();
    }
}
\onUpdate() {

}
\updateEx(updateT){
    if (updateT<0) updateT=0;
    for(_updateExc=(_updateExc||0)+updateT;_updateExc>=1;_updateExc--) {
        update();
    }
}
nowait \currentThreadGroup() {//@deprecated
    return _scheduler; //$currentThreadGroup;
}
\wait(t) {
    if(_thread) {
        _thread.suspend();
        if (t) {
            _thread.waitCount=t;
            if (_scheduler) _scheduler.addToNext(_thread);
        }
        __await null;
    } else if (_th) {
        if (t) {
            _th.waitCount=t;
        } else {
            if (_th.scheduled) _th.scheduled.unschedule(_th);
        }
    }
}
nowait \notify() {
    if (_th) {
        if (_th.scheduled) {
            _th.waitCount=0;
        } else {
            if (_scheduler) _scheduler.addToCur(_th);
        }
    }
}
\timeStop() {
    return $Boot.timeStop(this);
}
