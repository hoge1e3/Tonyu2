includes Fire;
p=$pat_r2b+5;
d=-90;
spd=5;
rot=5;
len=0;
pitch=floor(32/spd);
trace=[];
combo=0;
while(true) {
    if (t.active) {
        td=atan2(t.y-y,t.x-x);
        if (angleDiff(td,d)<0) {
            d-=rot;
        } 
        if (angleDiff(td,d)>0) {
            d+=rot;
        } 
        if (dist(t)<16) t.active=false;        
    }
    rotate=d;
    x+=cos(d)*spd;
    y+=sin(d)*spd;
    trace.unshift{x,y,entering,d};
    if (trace.length>maxLen*pitch) {
        las=trace.pop();
        if (las.entering) {
            die();
        }
    }
    if (trace.length>(len+1)*pitch) {
        len++;
        new Body{head:this, ofs:len*pitch};
        outFrom.bo();
    }
    if (!entering) {
        if (p!=$pat_r2b+7) {
            p = $pat_r2b+
            (all(Point).find\(p) {return p.mul>1;}.length>0 ? 6 : 5);
        }
        for (b in all(Body)) {
            if (dist(b)<16 && b.ofs>=5*pitch) {
                p=$pat_r2b+7;
                fire();
                dieEffect();
            }
        }
        if (allCrash(Rock).find \(r) 
        {return !r.capture;}.length>0) damage();
        if (screenOut()>100) $target.active=true;
        pp=crashTo(Pipe);
        //if (pp) print("Enter",pp,y,pp.y,spd,d, abs(angleDiff(d,90)));
        if (pp && y<pp.y-(5-spd) && abs(angleDiff(d,90))<60) {
            p=-1;
            entering=pp;
            $head=null;
            pp.accept(this);        
            t.active=false;
        }
    }
    if ($gov) dieEffect();
    update();
}
\getCrashRect() {
    return {x,y,width:16,height:16};
}
\damage() {
    for (var b in all(Body)) {
        b.p=$pat_r2b+3;
        new Bomb{x:b.x, y:b.y};
    }
    p=$pat_r2b+7;
}
\dieEffect() {
    $Screen.setBGColor("red");
    new RockEffect{x,y};
    p=-1;
    update();
    update();
    update();
    for (var e in all(Body)) {
        new RockEffect{x:e.x,y:e.y};
        e.die();
        update();
        update();
        update();
    }
    die();
    update();
}

/*\die(col) {
    print ("die",col);
    if (col) $Screen.setBGColor(col);
    super.die();
}*/
function screenOut(a) {
    //オブジェクトが画面外に出たかどうかを判定します。
    if (!a) a=0;
    var r=0;
    $viewX=0;$viewY=0;
    if (x<$viewX+a)               r+=$viewX+a-x;
    if (y<$viewY+a)               r+=$viewY+a-y;
    if (x>$screenWidth +$viewX-a) r+=x-($screenWidth +$viewX-a);
    if (y>$screenHeight+$viewY-a) r+=y-($screenHeight+$viewY-a);
    return r;
}
