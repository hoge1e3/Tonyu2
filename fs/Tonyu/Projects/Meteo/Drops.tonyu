extends DxChar;

// 3D空間上の座標(x2,y2,z)を決める
z=1000;
x2=(x-$screenWidth/2)*z;
y2=(y-$screenHeight/2)*z;
apz=rnd(300);
va=rnd(10)+1;
while (z>100) {
  // 3D空間上の座標から画面上の座標に変換する
  x=$screenWidth/2 + x2/z ;
  y=$screenHeight/2 + y2/z ;
  // 奥行きzにあわせて表示順序を設定する
  zOrder=-1000+z;
  // 移動
  z+=vz;x2+=vx;y2+=vy;
  vz-=0.5;vz=vz*0.99;        
  // zにあわせて大きさを決める
  scaleX=200/z;
  // 回転する
  angle+=va;
  if (!plc && z<900) {
    $mplayer.play($se_pass0);
    plc=1;
  }  
  if (!($Fighter.dying) && z>150 && z<150-vz) {
    // 自機との当たり判定
     if (crashTo($Fighter)) $Fighter.dying=1;
     else {
       // 自機をかすめた場合、近いほど高得点が入る
        di=dist(x-$Fighter.x,y-$Fighter.y)*0.1-3;//print(di);
        if (di>0.1) scad=trunc(10000/di/di ); 
        if (scad>300)     $mplayer.play($se_pass3);
        else if (scad>200) $mplayer.play($se_pass2); 
        else if (scad>50) $mplayer.play($se_pass1); 
        appear(new Score(x,y,scad));
        $score+=scad;
     }
  }      
 
  update();    
}
            