extends null;
native Sprites;
native Tonyu;
native Key;
native console;
native Math;

\new(x,y,p) {
    if (Tonyu.runMode) {
        var thg=Tonyu.currentProject.currentThreadGroup;
        if (thg) _th=thg.addObj(this);
    }
    if (typeof x=="object") Tonyu.extend(this, x);
    else if (typeof x=="number") {
        this.x=x;
        this.y=y;
        this.p=p;
    }
}

nowait \print(c) {
    console.log(c);
}
\update() {
    ifwait {
        _thread.suspend();
    }
}
nowait \getkey(k) {
    return Key.getkey(k);
}
nowait \crashTo(t) {
    return hitTo(t);
}
nowait \allCrash(t) {
    var res=[];
    if (!_sprite) return res;
    Sprites.sprites.forEach(\(s) {
        //print (s.owner instanceof t);
        if (s.owner!=this && 
        s.owner instanceof t && 
        s.crashTo(_sprite)) {
            res.push(s.owner);    
        }
    });
    return res;
}
nowait \hitTo(t) {
    if (!_sprite) return false;
    if (typeof t=="function") {
        return allCrash(t)[0];
    }
    if (t._sprite) {
        return _sprite.crashTo(t._sprite);
    }
    return false;
}
nowait \watchHit(typeA,typeB,onHit) {
    Sprites.watchHit(typeA , typeB, \(a,b) {
        onHit.apply(this,[a,b]);
    });
}
nowait \currentThreadGroup() {
    return Tonyu.currentProject.currentThreadGroup;
}
nowait \die() {
    if (_th) {
        _th.kill();
    }
    hide();
    isDead=true;
}
nowait \hide() {
    Sprites.remove(_sprite);
    _sprite=null;
}
nowait \rnd(r) {
    if (typeof r=="number") {
        return Math.floor(Math.random()*r);
    }
    return Math.random();
}
